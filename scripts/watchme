#!/usr/bin/env coffee
# Watcher scripts. Driving me nuts with inotify bollocks
# TODO - Add support for recursive regex matching

fs = require 'fs'
path = require 'path'
exec = require('child_process').exec

Array::includes = (e) ->
  for a in this
    return true if a == e
  return false

Array::splitOn = (e) ->
  hit = false; this.reduce ((a,b) ->
    hit ||= (b == e); a[+hit].push b; a), [[],[]]

# Reformat args
args = process.argv[2..]
try
  [files, [_, cmds...]] = args.splitOn('-r')
  cmd = cmds.join ' '
  [files, flags] = files.reduce ((a,b) ->
    a[+/^-[^r]$/.test(b)].push b; a), [[],[]]
catch e
  process.stderr.write 'Invalid args'
  exit 1

# Deals with the regex generation
if flags.includes '-i'
  rexedFiles = []
  for fpath in files
    [dir, rexstr] = [path.dirname(fpath), path.basename(fpath)]
    rex = new RegExp rexstr
    for f in fs.readdirSync dir
      rexedFiles.push path.join(dir, f) if rex.test f
  files = rexedFiles

if flags.includes '-c'
  cmd = "clear; #{cmd}"

# Begin to watch all files
for f in files
  console.log "Watching file: #{f}"
  fs.watchFile f, {interval: 500}, (event, filename) ->
    exec "#{cmd}", (err, stdout, stderr) ->
      process.stdout.write stdout
      if stderr != ''
        process.stdout.write ' >> stderr below'
        process.stderr.write stderr
     
